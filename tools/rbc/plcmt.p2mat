#!/bin/bash
#
# Converts a list of coordinates "xc, yc, zc" to a list of 4x4
# transformation matrixes

awk '
function usg() {
    msg("usage: <stream of positions> | plcmt.p2mat <scale> <angle/rnd>")
    msg("  prints 4x4 affine matrixes from coordinates; angle is rotation around OX")
    msg("  if angle=rnd angle is random")
    exit
}

function cell(x, y, z, p) {
    f0 = "%+9.3e"
    f = f0 " " f0 " " f0 " " f0 "\n"
    printf f,  s,        0,         0, x
    printf f,  0, s*cos(p), -s*sin(p), y
    printf f,  0, s*sin(p),  s*cos(p), z
    printf f,  0,        0,         0, 1
}

BEGIN {
    if (ARGV[1] == "-h") usg()
    s = ARGC > 1 ? ARGV[1] : 1; shift()
    p = ARGC > 1 ? ARGV[1] : 0; shift()
    print s, p
}

{
    x = $1; y = $2; z = $3
    if (p == "rnd") cell(x, y, z, rnd())
    else            cell(x, y, z,     p)
}

function rnd() {
    pi = 3.1415926
    return 2*pi*rand()
}

function msg(s) { printf "%s\n", s | "cat >&2" }
function shift(  i) { for (i = 2; i < ARGC; i++) ARGV[i-1] = ARGV[i]; ARGC-- }

' "$@"
