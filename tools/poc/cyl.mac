/*

Intersection of a point and a cylinder Cylinder is centered at (x=0,
z=0), infinite in `y' and radius is 1

*/

load("format") $
load("cgrind") $

[X, Y, Z]: [1, 2, 3]$

/* make a vector */
mv(P)::=buildq([l: P,
  r: create_list(concat(P, d), d,  ['x, 'y, 'z])],
  l: r) $

alias(cg, cgrind)$

mv(R0)$ mv(R1)$ /* Position of a point */

R : R0*(1-t) + R1*t;
x : R[X];
z : R[Z];
eq_t: x^2 + z^2 - 1; /* cylinder */
eq_t: format(eq_t, %poly(t))$

(cg('a=coeff(eq_t, t, 2)),
  cg('b=coeff(eq_t, t, 1)),
  cg('c=coeff(eq_t, t, 0)),
  cg('D=b^2 - 4*a*c),
  grind('t0=(-b + sqrt(D))/(2*a)),
  grind('t1=(-b - sqrt(D))/(2*a))) $
