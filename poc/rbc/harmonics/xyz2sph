#!/usr/bin/awk -f

# cartesian to spherical

BEGIN { ini() }

NF {
    # p : [0:2*pi]
    # t : [0:  pi]
    main()
}

function main(   i, x, y, z, p, t, r) {
    x = $++i; y = $++i; z = $++i

    p = atan2(y, x)
    t = atan2(z, sqrt(x^2 + y^2))
    r = sqrt(x^2 + y^2 + z^2)

    p = p < 0 ? p + 2*pi : p
    t = pi/2 - t

    ini_pr()
    pr(p); pr(t); pr(r)
    while (i <= NF) pr($++i)
    fin_pr()
}

function ini() { pi = 3.141592653589793 }

# print
function ini_pr() { cnt = 0 }
function pr(r) {
    printf "%s", r
    if (++cnt < NF) printf " "
}
function fin_pr() { printf "\n" }
