function off_read(file,   l, r, i) { # sets NV, NT, [XX, YY, ZZ], [T0, T1, T2]
    r = getline < file
    if (bad(r)) err("fail to read file: " file)
    if (!eq($0, "OFF")) err("not an OFF file")
    r = getline < file
    if (bad(r)) err("fail to read file: " file)
    NV = $1; NT = $2
    for (i = 0; i < NV; i++) {
	r = getline < file
	if (bad(r)) err("fail to read verts: " file)
	XX[i] = $1; YY[i] = $2; ZZ[i] = $3
    }
    for (i = 0; i < NT; i++) {
	r = getline < file
	if (bad(r)) err("fail to read tris: " file)
	T0[i] = $2; T1[i] = $3; T2[i] = $4
    }
    close(file)
}

function off_write(file,  ne, nvp, m) {
    ne = 0; nvp = 3
    printf "OFF\n" > file
    printf "%d %d %d\n", NV, NT, ne > file
    for (m = 0; m < NV; m++)
	printf "%.14e %.14e %.14e\n", XX[m], YY[m], ZZ[m] > file
    for (m = 0; m < NT; m++)
	printf "%d %d %d %d\n", nvp, T0[m], T1[m], T2[m] > file
}
