load("vect")   $
load("pdiff")  $
load("scifac") $
ratprint: false $

(X: 1, Y: 2, Z: 3) $

assume(sin(u) > 0);

C0: 0.2072; C2: 2.0026; C4: -1.12275 $
a: ('C4+'C2+'C0)/2 $ b: -(2*'C4+'C2)/2$ c: 'C4/2$
F(q):= 'a * q + 'b * q^3 + 'c * q^5;

cross(a, b):=express(a ~ b) $
dot(a, b):=express(a . b) $

x: sin(u)*cos(v) $
y: sin(u)*sin(v) $
z: F(cos(u))   $

r: [x, y, z]   $
ru: diff(r, u) $
rv: diff(r, v) $

guu: dot(ru, ru) $
guv: dot(ru, rv) $
gvv: dot(rv, rv) $
g  : determinant(matrix([guu, guv], [guv, gvv])) $

ruu: diff(r, u, 2) $
rvv: diff(r, v, 2) $
ruv: diff(diff(r, v), u) $
n   :  cross(ru, rv) / sqrt(g) $

c1: fullratsimp(trigsimp(dot(ruu, n))) / guu $
c1: trigsimp(c1) $
M: trigsimp(dot(ruv, n)) $
if M # 0 then error("4: M should be zero") $
c2: dot(rvv, n)              / gvv $
c2: trigsimp(c2) $

e: (c1 + c2)^2;

integrate(e * g, 'u, 0, %pi);

matchdeclare(x, true);
defrule(r_F0, F(x),             F0(x));
defrule(r_F1, pderivop(F,1)(x), F1(x));
defrule(r_F2, pderivop(F,2)(x), F2(x));

conv(e):=apply1(e, r_F0, r_F1, r_F2);
c1: conv(c1) $
c2: conv(c2) $


