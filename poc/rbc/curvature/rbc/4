load("vect")   $
load("pdiff")  $
load("scifac") $
ratprint: false $

(X: 1, Y: 2, Z: 3) $

assume(sin(u) > 0);

a0: 0.0518; a1: 2.0026; a2: -4.491;
C5: a2/16; C3: -(a2+2*a1)/8; C1: (a2+4*a1+16*a0)/16;

F(c):= C5 * c^5 + C3 * c^3 + C1 * c;
/* F(c):= 1/2*c; */

matchdeclare(al, all);
defrule(r_sin0, sin(al)^2, 1 - cos(al)^2);
defrule(r_sin1, sin(al)^4, (1 - cos(al)^2)^2);
to_cos(e):=apply1(e, r_sin0, r_sin1);

cross(a, b):=express(a ~ b);
dot(a, b):=express(a . b);

x: 1/2*sin(u)*cos(v);
y: 1/2*sin(u)*sin(v);
z: F(cos(u));

r: [x, y, z];
ru: diff(r, u);
rv: diff(r, v);

ruu: diff(r, u, 2);
rvv: diff(r, v, 2);
ruv: diff(diff(r, v), u);

n:  trigsimp(cross(trigsimp(ru), trigsimp(rv)));
nn: dot(n, n);
nn: to_cos(gcfac(nn));

guu: dot(ru, ru);
gvv: dot(rv, rv);

L: fullratsimp(trigsimp(dot(ruu, n))) / guu;
M: trigsimp(dot(ruv, n));
if M # 0 then error("4: M should be zero") $
N: trigsimp(dot(rvv, n))              / gvv;

D: 12.08 $
ju: "../data/julicher.txt" $
ju: read_nested_list(ju) $
fun(l):=[l[1]/D, 4*l[4]*D^2] $
ju: map(fun, ju) $

ka: "../data/kantor.txt" $
ka: read_nested_list(ka) $
fun(l):=[l[1]/D, l[4]*D^3] $
ka: map(fun, ka) $

o: "main" $
printf(true, "write: ~a.png~%", o)$
draw2d(nticks=100, terminal = 'pdf, file_name  = o,
  'xlabel = "R", 'ylabel = "energy", 'key_pos = 'top_left, 'line_width = 3,
  'key = "Julicher",   'color = 'red,   points(ju),
  'key = "Kantor",     'color = 'green, points(ka),
  'key = "analit.",    'color = 'blue,  parametric(x, (1/2*(L + N)/sqrt(nn))^2, u, 0, %pi/2),
  'key = "bi-concave profile",    'color = 'black, parametric(x, 50*z, u, 0, %pi/2)
  ), v = 0;
