#!/usr/bin/awk -f

# Panton, R. L. (2006). Incompressible flow. John Wiley & Sons.
# 11.4. STOKESâ€™S OSCILLATING PLATE

function ini0() { # defaults
    t   = 1
    om  = 1
    mu  = 1
    vel0 = 1
    lo = 0; hi = 10; n = 100
}

function arg0(k, v) { # key-value
    if       (k == "t")    t = v
    else if  (k == "om")  om = v
    else if  (k == "mu")  mu = v
    else if  (k == "v")  vel0 = v
    else if  (k == "lo")  lo = v
    else if  (k == "hi")  hi = v
    else if  (k == "n")   n  = v
    else err("unknown parameter: " k)
}

function arg1(a0,   a, k, v) {
    split(a0, a, "=")
    k = a[1]; v = a[2]
    arg0(k, v)
}

function arg(   i, a) {
    for (i = 1; i in ARGV; i++) arg1(ARGV[i])
}

function ini() {
    ini0()
    arg()
}

function x2i(i) { return lo + (hi - lo)/(n - 1)*i }

function v0(x, t) {
    x /= sqrt(2)
    return exp(-x) * sin(t - x)
}

function v(x, t,   vel) {
    x = x/sqrt(mu/om)
    t = om*t
    vel = v0(x, t)
    return vel0*vel
}

BEGIN {
    ini()
    for (i = 0; i < n; i++) {
	x  = x2i(i)
	vel = v(x, t)
	print x, vel
    }
}


function err(s) { printf "solution: " s "\n" | "cat >&2"; exit(2) }
